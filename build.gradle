plugins {
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.13'
    id 'org.owasp.dependencycheck' version '7.4.4'
}

group = 'uk.nhs.prm.deductions'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

//Without this task two jars are built, the additional "-plain.jar" is not needed
// for more details refer to: https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
jar {
    enabled = false
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-aop'

    implementation 'org.yaml:snakeyaml:2.2'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.4'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.68'
    implementation 'com.nimbusds:nimbus-jose-jwt:9.30.2'
    implementation 'org.jetbrains:annotations:20.1.0'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.json:json:20231013'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'

    implementation('io.netty:netty-buffer') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-codec') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-codec-http') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-codec-http2') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-common') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-handler') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-resolver') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-transport') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-transport-classes-epoll') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-transport-native-unix-common') { version { strictly '4.1.104.Final' } }

    implementation platform('software.amazon.awssdk:bom:2.18.41')
    implementation 'software.amazon.awssdk:ssm'

    compileOnly 'org.projectlombok:lombok:1.18.30'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.1'
}

springBoot {
    mainClass = 'uk.nhs.prm.deductions.pdsadaptor.PdsAdaptorApplication'
}

sourceSets {
    integration {
        compileClasspath += sourceSets.main.output
        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    testLogging.showStandardStreams = true
}

tasks.withType(Test) {
    useJUnitPlatform()
}

task integration(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    shouldRunAfter test
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    shouldRunAfter integration
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/model/**')
        }))
    }
}

spotbugsMain {
    ignoreFailures = true
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    enabled = false
}

spotbugsIntegration {
    ignoreFailures = true
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/integration/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

dependencyCheck {
    failBuildOnCVSS = 7
    suppressionFile = './dependency-checks-suppression.xml'
    analyzers {
          assemblyEnabled = false
    }
}

check.dependsOn integration

test.outputs.upToDateWhen {false}
integration.outputs.upToDateWhen {false}
