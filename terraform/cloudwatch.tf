locals {
  error_logs_metric_name       = "ErrorCountInLogs"
  pds_adaptor_metric_namespace = "PdsAdaptor"
}

resource "aws_cloudwatch_log_group" "log_group" {
  name = "/nhs/deductions/${var.environment}-${data.aws_caller_identity.current.account_id}/${var.component_name}"

  tags = {
    Environment = var.environment
    CreatedBy= var.repo_name
  }
}

resource "aws_cloudwatch_log_metric_filter" "log_metric_filter" {
  name           = "${var.environment}-${var.component_name}-error-logs"
  pattern        = "{ $.level = \"ERROR\" }"
  log_group_name = aws_cloudwatch_log_group.log_group.name

  metric_transformation {
    name          = local.error_logs_metric_name
    namespace     = local.pds_adaptor_metric_namespace
    value         = 1
    default_value = 0
  }
}

resource "aws_cloudwatch_metric_alarm" "error_log_alarm" {
  alarm_name                = "${var.environment}-${var.component_name}-error-logs"
  comparison_operator       = "GreaterThanThreshold"
  threshold                 = "0"
  evaluation_periods        = "1"
  period                    = "60"
  metric_name               = local.error_logs_metric_name
  namespace                 = local.pds_adaptor_metric_namespace
  statistic                 = "Sum"
  alarm_description         = "This alarm monitors errors logs in ${var.component_name}"
  treat_missing_data        = "notBreaching"
  actions_enabled           = "true"
  alarm_actions             = [data.aws_sns_topic.alarm_notifications.arn]
  ok_actions                = [data.aws_sns_topic.alarm_notifications.arn]
}

resource "aws_cloudwatch_metric_alarm" "alb_http_errors" {
  alarm_name                = "${var.repo_name} 5xx errors"
  comparison_operator       = "GreaterThanOrEqualToThreshold"
  evaluation_periods        = "1"
  metric_name               = "HTTPCode_Target_5XX_Count"
  namespace                 = "AWS/ApplicationELB"
  period                    = "60"
  statistic                 = "Average"
  threshold                 = "1"
  alarm_description         = "This metric monitors number of 5xx http status codes associated with ${var.repo_name}"
  treat_missing_data        = "notBreaching"
  dimensions                = {
    LoadBalancer = aws_alb.alb-internal.arn_suffix
  }
  alarm_actions             = [data.aws_sns_topic.alarm_notifications.arn]
  ok_actions                = [data.aws_sns_topic.alarm_notifications.arn]
}

resource "aws_cloudwatch_metric_alarm" "alb_service_down_errors" {
  alarm_name                = "${var.repo_name} service down"
  comparison_operator       = "LessThanThreshold"
  evaluation_periods        = "1"
  metric_name               = "HealthyHostCount"
  namespace                 = "AWS/ApplicationELB"
  period                    = "60"
  statistic                 = "Average"
  threshold                 = "1"
  alarm_description         = "This metric monitors the health of ${var.repo_name}"
  treat_missing_data        = "breaching"
  datapoints_to_alarm       = "1"
  dimensions                = {
    TargetGroup = aws_alb_target_group.internal-alb-tg.arn_suffix
    LoadBalancer = aws_alb.alb-internal.arn_suffix
  }
  alarm_actions             = [data.aws_sns_topic.alarm_notifications.arn]
  ok_actions                = [data.aws_sns_topic.alarm_notifications.arn]
}


data "aws_sns_topic" "alarm_notifications" {
  name = "${var.environment}-alarm-notifications-sns-topic"
}